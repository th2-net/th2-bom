plugins {
    id 'java-platform'
    id 'maven-publish'
    id "io.github.gradle-nexus.publish-plugin" version "1.0.0"
    id 'signing'
    id "org.owasp.dependencycheck" version "8.3.1"
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
}

ext {
    slf4jVersion = '2.0.7'
    log4jVersion = '2.20.0'
    grpcVersion = '1.56.0'
    protobufVersion = '3.23.3'
    jacksonVersion = '2.15.2'
    prometheusVersion = '0.16.0'
    cassandraDriver = '4.15.0'
    nettyVersion = '4.1.94.Final'
}

group = 'com.exactpro.th2'
version = release_version

dependencyLocking {
    lockAllConfigurations()
}

repositories {
    mavenCentral()
}

javaPlatform {
    allowDependencies()
}

dependencyCheck {
    formats = ['SARIF', 'JSON', 'HTML']
    failBuildOnCVSS = 5
    analyzers {
        assemblyEnabled = false
        nugetconfEnabled = false
        nodeEnabled = false
    }
}

dependencies {
    api platform('org.jetbrains.kotlin:kotlin-bom:1.6.21')
    api platform("io.grpc:grpc-bom:$grpcVersion")
    api platform("com.fasterxml.jackson:jackson-bom:$jacksonVersion")
    api platform("com.datastax.oss:java-driver-bom:$cassandraDriver")
    api platform("io.netty:netty-bom:$nettyVersion")

    constraints {
        // Libraries
        api("com.rabbitmq:amqp-client:5.9.0")

        api("org.jetbrains:annotations:23.0.0")
        api("io.netty:netty-codec-http:4.1.86.Final") {
            because("there is no version for grpc-bom where `non-infected` version is used.")
        }

        api("org.apache.commons:commons-text:1.10.0")
        api("org.apache.commons:commons-lang3:3.12.0")
        api('commons-io:commons-io:2.11.0')
        api("commons-cli:commons-cli:1.5.0")
        api("org.apache.commons:commons-collections4:4.4")

        api("com.google.guava:guava:32.0.1-jre")

        api("com.google.protobuf:protobuf-java-util:${protobufVersion}")
        api("com.google.protobuf:protoc:${protobufVersion}")

        api("org.slf4j:slf4j-api:${slf4jVersion}")
        api("org.apache.logging.log4j:log4j-slf4j2-impl:${log4jVersion}")
        api("org.apache.logging.log4j:log4j-1.2-api:${log4jVersion}")
        api("org.apache.logging.log4j:log4j-api:${log4jVersion}")
        api("org.apache.logging.log4j:log4j-core:${log4jVersion}")

        // Prometheus FIXME: remove when we have a facade in common
        api("io.prometheus:simpleclient:${prometheusVersion}")
        api("io.prometheus:simpleclient_hotspot:${prometheusVersion}")
        api("io.prometheus:simpleclient_httpserver:${prometheusVersion}")
        api("io.prometheus:simpleclient_log4j2:${prometheusVersion}")
        api("io.prometheus:simpleclient_log4j:${prometheusVersion}")
    }
}

// conditionals for publications
tasks.withType(PublishToMavenRepository).configureEach {
    onlyIf {
        (repository == publishing.repositories.nexusRepository &&
                project.hasProperty('nexus_user') &&
                project.hasProperty('nexus_password') &&
                project.hasProperty('nexus_url')) ||
                (repository == publishing.repositories.sonatype &&
                        project.hasProperty('sonatypeUsername') &&
                        project.hasProperty('sonatypePassword'))
    }
}
tasks.withType(Sign).configureEach {
    onlyIf {
        project.hasProperty('signingKey') &&
                project.hasProperty('signingPassword')
    }
}
// disable running task 'initializeSonatypeStagingRepository' on a gitlab
tasks.configureEach { task ->
    if (task.name == 'initializeSonatypeStagingRepository' &&
            !(project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword'))
    ) {
        task.enabled = false
    }
}

publishing {
    publishing {
        publications {
            th2Platform(MavenPublication) {
                from components.javaPlatform
                pom {
                    name = rootProject.name
                    description = rootProject.description
                    url = vcs_url
                    scm {
                        url = vcs_url
                    }
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.    txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'developer'
                            name = 'developer'
                            email = 'developer@exactpro.com'
                        }
                    }
                    scm {
                        url = vcs_url
                    }
                }
            }
        }
    }
    repositories {
        //Nexus repo to publish from gitlab
        maven {
            name = 'nexusRepository'
            credentials {
                username = project.findProperty('nexus_user')
                password = project.findProperty('nexus_password')
            }
            url = project.findProperty('nexus_url')
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

signing {
    String signingKey = findProperty("signingKey")
    String signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.th2Platform
}