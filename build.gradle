import com.github.jk1.license.filter.LicenseBundleNormalizer
import com.github.jk1.license.render.JsonReportRenderer

plugins {
    id 'java-platform'
    id 'maven-publish'
    id "io.github.gradle-nexus.publish-plugin" version "2.0.0"
    id 'signing'
    id 'org.owasp.dependencycheck' version "10.0.4"
    id "com.gorylenko.gradle-git-properties" version "2.4.2"
    id 'com.github.jk1.dependency-license-report' version "2.9"
    id 'de.undercouch.download' version "5.6.0"
}

ext {
    slf4jVersion = '2.0.16'
    log4jVersion = '2.23.1'
    grpcVersion = '1.66.0'
    protobufVersion = '3.25.4' // gRPC 1.66 libraries uses protobuf 3.+
    jacksonVersion = '2.17.2'
    prometheusVersion = '0.16.0'
    cassandraDriver = '4.17.0'
    nettyVersion = '4.1.112.Final'
}

group = 'com.exactpro.th2'
version = release_version

dependencyLocking {
    lockAllConfigurations()
}

repositories {
    mavenCentral()
}

javaPlatform {
    allowDependencies()
}

dependencyCheck {
    formats = ['SARIF', 'JSON', 'HTML']
    failBuildOnCVSS = 5

    nvd {
        apiKey = project.findProperty("nvdApiKey") as String
        delay = Integer.valueOf(project.findProperty("nvdDelay") as String)
    }

    analyzers {
        assemblyEnabled = false
        nugetconfEnabled = false
        nodeEnabled = false
    }
}

dependencies {
    api platform('org.jetbrains.kotlin:kotlin-bom:1.8.22')
    api platform("io.grpc:grpc-bom:$grpcVersion")
    api platform("com.fasterxml.jackson:jackson-bom:$jacksonVersion")
    api platform("com.datastax.oss:java-driver-bom:$cassandraDriver")
    api platform("io.netty:netty-bom:$nettyVersion")
    api platform("org.apache.logging.log4j:log4j-bom:$log4jVersion")
    api platform("org.slf4j:slf4j-bom:${slf4jVersion}")

    constraints {
        // Libraries
        api("com.rabbitmq:amqp-client:5.21.0")

        api("org.jetbrains:annotations:24.1.0")

        api("org.apache.commons:commons-text:1.12.0")
        api("org.apache.commons:commons-lang3:3.17.0")
        api('commons-io:commons-io:2.16.1')
        api("commons-cli:commons-cli:1.9.0")
        api("org.apache.commons:commons-collections4:4.4")

        api("com.google.guava:guava:33.3.0-jre")

        api("com.google.protobuf:protobuf-java-util:${protobufVersion}")
        api("com.google.protobuf:protoc:${protobufVersion}")

        api("org.slf4j:slf4j-api")

        // Prometheus FIXME: remove when we have a facade in common
        api("io.prometheus:simpleclient:${prometheusVersion}")
        api("io.prometheus:simpleclient_hotspot:${prometheusVersion}")
        api("io.prometheus:simpleclient_httpserver:${prometheusVersion}")
        api("io.prometheus:simpleclient_log4j2:${prometheusVersion}")
        api("io.prometheus:simpleclient_log4j:${prometheusVersion}")
    }
}

// conditionals for publications
tasks.withType(PublishToMavenRepository).configureEach {
    onlyIf {
        (repository == publishing.repositories.nexusRepository &&
                project.hasProperty('nexus_user') &&
                project.hasProperty('nexus_password') &&
                project.hasProperty('nexus_url')) ||
                (repository == publishing.repositories.sonatype &&
                        project.hasProperty('sonatypeUsername') &&
                        project.hasProperty('sonatypePassword'))
    }
}
tasks.withType(Sign).configureEach {
    onlyIf {
        project.hasProperty('signingKey') &&
                project.hasProperty('signingPassword')
    }
}
// disable running task 'initializeSonatypeStagingRepository' on a gitlab
tasks.configureEach { task ->
    if (task.name == 'initializeSonatypeStagingRepository' &&
            !(project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword'))
    ) {
        task.enabled = false
    }
}

publishing {
    publishing {
        publications {
            th2Platform(MavenPublication) {
                from components.javaPlatform
                pom {
                    name = rootProject.name
                    description = rootProject.description
                    url = vcs_url
                    scm {
                        url = vcs_url
                    }
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'developer'
                            name = 'developer'
                            email = 'developer@exactpro.com'
                        }
                    }
                    scm {
                        url = vcs_url
                    }
                }
            }
        }
    }
    repositories {
        //Nexus repo to publish from gitlab
        maven {
            name = 'nexusRepository'
            credentials {
                username = project.findProperty('nexus_user')
                password = project.findProperty('nexus_password')
            }
            url = project.findProperty('nexus_url')
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

signing {
    String signingKey = findProperty("signingKey")
    String signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.th2Platform
}

licenseReport {
    def licenseNormalizerBundlePath = "$buildDir/license-normalizer-bundle.json"

    if (!file(licenseNormalizerBundlePath).exists()) {
        download.run {
            src 'https://raw.githubusercontent.com/th2-net/.github/main/license-compliance/gradle-license-report/license-normalizer-bundle.json'
            dest "$buildDir/license-normalizer-bundle.json"
            overwrite false
        }
    }

    filters = [
            new LicenseBundleNormalizer(licenseNormalizerBundlePath, false)
    ]
    renderers = [
            new JsonReportRenderer('licenses.json', false),
    ]
    excludeOwnGroup = false
    allowedLicensesFile = new URL("https://raw.githubusercontent.com/th2-net/.github/main/license-compliance/gradle-license-report/allowed-licenses.json")
}