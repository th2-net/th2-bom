plugins {
    id 'java-platform'
    id 'maven-publish'
    id "io.github.gradle-nexus.publish-plugin" version "1.0.0"
    id 'signing'
}

ext {
    sharedDir           = file('shared')

    slf4jVersion        = '2.17.2'
    grpcVersion         = '1.48.1'
    protobufVersion     = '3.21.5'
    jacksonVersion      = '2.13.3'
    prometheusVersion   = '0.16.0'
    cassandraDriver     = '4.14.1'
}

group = 'com.exactpro.th2'
version = release_version

repositories {
    mavenCentral()
}

javaPlatform {
    allowDependencies()
}

dependencies {
    api platform('org.jetbrains.kotlin:kotlin-bom:1.6.21')
    api platform("io.grpc:grpc-bom:$grpcVersion")
    api platform('com.fasterxml.jackson:jackson-bom:2.13.3')

    constraints {
        // Libraries
        api("com.rabbitmq:amqp-client:5.9.0")
        api("com.datastax.oss:java-driver-core:$cassandraDriver")
        api("com.datastax.oss:java-driver-mapper-processor:$cassandraDriver")
        api("com.datastax.oss:java-driver-mapper-runtime:$cassandraDriver")
        api("com.datastax.oss:java-driver-query-builder:$cassandraDriver")

        api("org.jetbrains:annotations:23.0.0")

        api("org.apache.commons:commons-lang3:3.12.0")
        api('commons-io:commons-io:2.11.0')
        api("commons-cli:commons-cli:1.5.0")
        api("org.apache.commons:commons-collections4:4.4")

        api("com.google.guava:guava:31.1-jre")

        api("com.google.protobuf:protobuf-java-util:${protobufVersion}")
        api("com.google.protobuf:protoc:${protobufVersion}")

        api("org.apache.logging.log4j:log4j-slf4j-impl:${slf4jVersion}")
        api("org.apache.logging.log4j:log4j-1.2-api:${slf4jVersion}")
        api("org.apache.logging.log4j:log4j-api:${slf4jVersion}")
        api("org.apache.logging.log4j:log4j-core:${slf4jVersion}")

        // Prometheus FIXME: remove when we have a facade in common
        api("io.prometheus:simpleclient:${prometheusVersion}")
        api("io.prometheus:simpleclient_hotspot:${prometheusVersion}")
        api("io.prometheus:simpleclient_httpserver:${prometheusVersion}")
        api("io.prometheus:simpleclient_log4j2:${prometheusVersion}")
        api("io.prometheus:simpleclient_log4j:${prometheusVersion}")
    }
}

// conditionals for publications
tasks.withType(PublishToMavenRepository) {
    onlyIf {
        (repository == publishing.repositories.nexusRepository &&
            project.hasProperty('nexus_user') &&
            project.hasProperty('nexus_password') &&
            project.hasProperty('nexus_url')) ||
        (repository == publishing.repositories.sonatype &&
            project.hasProperty('sonatypeUsername') &&
            project.hasProperty('sonatypePassword')) ||
        (repository == publishing.repositories.localRepo)
    }
}
tasks.withType(Sign) {
    onlyIf { project.hasProperty('signingKey') &&
        project.hasProperty('signingPassword')
    }
}
// disable running task 'initializeSonatypeStagingRepository' on a gitlab
tasks.whenTaskAdded {task ->
    if(task.name.equals('initializeSonatypeStagingRepository') &&
        !(project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword'))
    ) {
        task.enabled = false
    }
}

publishing {
    publishing {
        publications {
            th2Platform(MavenPublication) {
                from components.javaPlatform
                pom {
                  name = rootProject.name
                  description = rootProject.description
                  url = vcs_url
                  scm {
                    url = vcs_url
                  }
                  licenses {
                    license {
                      name = 'The Apache License, Version 2.0'
                      url = 'http://www.apache.org/licenses/LICENSE-2.0.    txt'
                    }
                  }
                  developers {
                    developer {
                      id = 'developer'
                      name = 'developer'
                      email = 'developer@exactpro.com'
                    }
                  }
                  scm {
                    url = vcs_url
                  }
               }
            }
        }
    }
    repositories {
        maven {
            name = 'localRepo'
            url = sharedDir
        }
//Nexus repo to publish from gitlab
        maven {
            name = 'nexusRepository'
            credentials {
                username = project.findProperty('nexus_user')
                password = project.findProperty('nexus_password')
            }
            url = project.findProperty('nexus_url')
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.th2Platform
}

clean {
    delete sharedDir
}

